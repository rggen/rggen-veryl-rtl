import  rggen_rtl_pkg::*;

pub module rggen_avalon_adapter #(
  param ADDRESS_WIDTH:        u32                 = 8,
  param LOCAL_ADDRESS_WIDTH:  u32                 = 8,
  param BUS_WIDTH:            u32                 = 32,
  param REGISTERS:            u32                 = 1,
  param PRE_DECODE:           bbool               = false,
  param BASE_ADDRESS:         bit<ADDRESS_WIDTH>  = '0,
  param BYTE_SIZE:            u32                 = 256,
  param ERROR_STATUS:         bbool               = false,
  param DEFAULT_READ_DATA:    bit<BUS_WIDTH>      = '0,
  param INSERT_SLICER:        bbool               = false
)(
  i_clk:        input   clock,
  i_rst:        input   reset,
  avalon_if:    modport rggen_avalon_if::agent,
  register_if:  modport rggen_register_if::host[REGISTERS]
) {
  inst  bus_if:       rggen_bus_if #(ADDRESS_WIDTH, BUS_WIDTH);
  var   waitrequest:  logic;
  var   response:     logic<2>;
  var   readdata:     logic<BUS_WIDTH>;

  always_comb {
    bus_if.valid      = (avalon_if.read || avalon_if.write) && waitrequest;
    bus_if.access     = if avalon_if.read ? rggen_access::READ : rggen_access::WRITE;
    bus_if.address    = avalon_if.address;
    bus_if.write_data = avalon_if.writedata;
    bus_if.strobe     = avalon_if.byteenable;
  }

  always_comb {
    avalon_if.waitrequest = waitrequest;
    avalon_if.response    = response;
    avalon_if.readdata    = readdata;
  }

  always_ff {
    if_reset {
      waitrequest = '1;
    } else if bus_if.ack() {
      waitrequest = '0;
    } else {
      waitrequest = '1;
    }
  }

  always_ff {
    if bus_if.ack() {
      response  = bus_if.status;
      readdata  = bus_if.read_data;
    }
  }

  //  Adapter common
  inst  u_adapter_common: rggen_adapter_common #(
    ADDRESS_WIDTH:        ADDRESS_WIDTH,
    LOCAL_ADDRESS_WIDTH:  LOCAL_ADDRESS_WIDTH,
    BUS_WIDTH:            BUS_WIDTH,
    STROBE_WIDTH:         BUS_WIDTH / 8,
    REGISTERS:            REGISTERS,
    PRE_DECODE:           PRE_DECODE,
    BASE_ADDRESS:         BASE_ADDRESS,
    BYTE_SIZE:            BYTE_SIZE,
    USE_READ_STROBE:      true,
    ERROR_STATUS:         ERROR_STATUS,
    DEFAULT_READ_DATA:    DEFAULT_READ_DATA,
    INSERT_SLICER:        INSERT_SLICER
  )(
    i_clk:        i_clk,
    i_rst:        i_rst,
    bus_if:       bus_if,
    register_if:  register_if
  );
}
