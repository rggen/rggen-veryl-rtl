import rggen_rtl_pkg::*;

pub module rggen_bit_field_w01trg #(
  param TRIGGER_VALUE:  bit = '0,
  param WIDTH:          u32 = 1
)(
  i_clk:        input   clock,
  i_rst:        input   reset,
  bit_field_if: modport rggen_bit_field_if::bit_field,
  i_value:      input   logic<WIDTH>  = '0,
  o_trigger:    output  logic<WIDTH>  = _
){
  var trigger       : logic<WIDTH>;
  var assert_trigger: logic<WIDTH>;

  always_comb {
    bit_field_if.read_data  = i_value;
    bit_field_if.value      = trigger;
  }

  always_comb {
    o_trigger = trigger;
  }

  always_comb {
    for i: u32 in 0..WIDTH {
      assert_trigger[i] =
        bit_field_if.write_valid && bit_field_if.mask[i] &&
          bit_field_if.write_data[i] == TRIGGER_VALUE;
    }
  }

  always_ff {
    if_reset {
      trigger = '0;
    } else {
      for i: u32 in 0..WIDTH {
        if assert_trigger[i] {
          trigger[i] = '1;
        } else if trigger[i] {
          trigger[i] = '0;
        }
      }
    }
  }
}
