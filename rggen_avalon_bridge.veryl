import  rggen_rtl_pkg::*;

module rggen_avalon_bridge #(
  param ADDRESS_WIDTH:  u32 = 16,
  param READ_STROBE:    bit = 1
)(
  bus_if:     modport rggen_bus_if::slave,
  avalon_if:  modport rggen_avalon_if::host
) {
  always_comb {
    avalon_if.read        = bus_if.valid && (bus_if.access == rggen_access::READ);
    avalon_if.write       = bus_if.valid && (bus_if.access != rggen_access::READ);
    avalon_if.address     = bus_if.address;
    avalon_if.byteenable  = if bus_if.access != rggen_access::READ { bus_if.strobe }
                            else if READ_STROBE                    { bus_if.strobe }
                            else                                   { '1            };
    avalon_if.writedata   = bus_if.write_data;
  }

  always_comb {
    bus_if.ready      = bus_if.valid && (!avalon_if.waitrequest);
    bus_if.status     = avalon_if.response as rggen_status;
    bus_if.read_data  = avalon_if.readdata;
  }
}
